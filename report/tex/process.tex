\chapter{Process}
In this chapter the process of exploring the different I/O styles is laid out. The first section explains the approach and an analysis of the I/O primitives required for implementing the protocol.

\section{Approach}
To explore different styles the foundations of the SCTP protocol\cite{_rfc_????} are implemented in Haskell using these styles. The implementations yield valuable insight into which I/O functions are used and how they interact with eachother.
To make implementing the different I/O styles a simple and fast process a clear separation has been made between pure and I/O functions. All non-I/O functions have been extracted into utility functions and placed into different files so they can be reused by the different implementations. This clear separation also allows clear vision on what I/O actions are performed by each function, without distractions by complex pure functions.

\begin{lstlisting}[caption={An example of a shared pure function}]
deserializeMessage :: ByteString -> Message
deserializeMessage bytes = Message header chunks
    where
        (header_bytes, chunk_bytes) =
            BS.splitAt (fromIntegral commonHeaderSize) bytes
        header = deserializeCommonHeader $
            BL.fromChunks [header_bytes]
        chunks = deserializeChunks $
            BL.fromChunks [chunk_bytes]
\end{lstlisting}

During the implementation the need for the following I/O actions was discovered:

\begin{itemize}

\item To receive and send data, the basic receiving and sending actions are performed.

\item To allow for multiple sockets to be created on the stack from different threads, their state needs to be concurrently accessible through some concurrency primitives like acquiring and waiting for locks.

\item In cases of network or hardware failure exceptions are generated that must be caught and handled. Also when these failures are not reported but are noticeable through data corruption.

\item To get information from the system like the time or a free portnumber foreign function calls must be made.
\end{itemize}

These actions cover the four categories of the awkward squad \cite{jones_tackling_2009}. 
This confirms the idea that the network protocol implementation is a good subject to test the I/O styles in.
\section{Monadic Style}
\paragraph{Architecture}
The monadic style implementation has an architecture inspired by events and event handlers. At the base there is a thread that does a blocking read on the message source. The message is then passed through several functions that decode the message in steps, when the message is valid and it is of interest to the application layer, an event handler is called with the relevant data.

\begin{lstlisting}[caption={The eventhandler is called when a payload is received}]
handlePayload :: Association -> Payload -> IO(Association)
handlePayload association@Association{..} payload = do 
    acknowledge association payload
    (eventhandler associationSocket) $
	    Data association $ userData payload
    return association
\end{lstlisting}

\paragraph{Operation}
The interfacing program first starts the stack, which is the interface to the SCTP protocol implementation that is normally run by the operating system. The stack opens an operating system socket and spawns a new thread that performs the blocking read operation on this socket. Whenever a packet arrives it then goes through a list of registered sockets to see if any socket is open to data on the port the packet is destined for and then calls the socketAcceptMessage function with that socket and the packet as parameters.

\begin{lstlisting}[caption={The stack loop}]
stackLoop :: SCTP -> IO ()
stackLoop stack = forever $ do
    (bytes, peerAddress) <- NSB.recvFrom 
        (underLyingSocket stack) maxMessageSize
    let message = deserializeMessage bytes
    let h = header message
    let destination = (address stack,
                       destinationPortNumber h)
    sockets <- readMVar (instances stack)
    case Map.lookup destination sockets of
        Just socket -> socketAcceptMessage socket
                       (ipAddress peerAddress) message
        Nothing -> return ()
\end{lstlisting}

The interfacing application can register a socket at the stack by either calling the 'connect' or 'listen' function with an address and an event handler function as parameters.

The 'socketAcceptMessage' function checks and decodes the message calls the handle function for the particular type of the message. The handler functions perform actions like creating associations, transferring data and tearing associations down. An association here represents a connection between two peers.

When an action is performed that the interfacing application could be interested in, such as an incoming connection or received data, the event handler the application registered is called with an event object with relevant information.

\begin{lstlisting}[caption={socketAcceptMessage decides what to do with a message}]
socketAcceptMessage :: Socket -> IpAddress -> Message -> IO()
socketAcceptMessage socket address message = do
    (eventhandler socket) (OtherEvent message)
    -- Drop packet if verifyChecksum fails
    when (verifyChecksum message) $ do
        let tag = verificationTag $ header message
        -- verification tag is 0, so message MUST be INIT
        if tag == 0 
            then handleInit socket address message
            else do
                let allChunks@(firstChunk : restChunks) =
                    chunks message
                let toProcess
                    | chunkType firstChunk ==
                        cookieEchoChunkType = restChunks
                    | otherwise = allChunks
                when (chunkType firstChunk == 
                  cookieEchoChunkType) $
                    handleCookieEcho socket address message
                unless (toProcess == []) $ do
                    processOnSocket socket tag toProcess
\end{lstlisting}

Various actions in the protocol have associated timers. For example when a connection is initiated the connecting side starts a timer. If an acknowledgement of the connection attempt is not received before the timer runs out the attempt is retransmitted.
The timer runs in a separate thread which repeatedly sleeps a few milliseconds, wakes up and checks whether enough time has passed. If it has then it calls whatever function has been assigned to it. 

\begin{lstlisting}[caption={The init retransmission function}]
initRetransmit :: Association -> Message -> Integer -> IO ()
initRetransmit association message attempt = do
    if attempt < defaultMaxInitRetransmits
      then do
          socketSendMessage socket 
            (ipAddress peerAddr, portNumber peerAddr)
              message
          registerTimer timer 0
            (initRetransmit association message $ attempt+1)
              timeOut
          return ()
      else do
          closeConnection association
          (eventhandler socket) $ Error "Could not connect."
          return ()
\end{lstlisting}

Because timers run in a separate thread that runs concurrently with the packet receiving thread and both threads modify the state of associations the need for threadsafe communication of state arises. A simple way of threadsafe communication in Haskell is by using the MVar. It represents a mutable variable that can be taken and put back with an IO operation. When the variable is taken, access to the variable is atomically blocked for other threads until the variable is put back. 

\paragraph{Analysis}
Since implementation has a clear separation between pure and I/O functions it is easy to see what I/O actions are performed by each function. 

\begin{itemize}
\item To receive and send data, an operating system socket is created and the sendTo and recvFrom functions are called. Whenever a new socket is created a portnumber is also requested from the operating system.
\item To allow for multiple sockets to be created on the stack, a separate thread is spawned and the list of sockets is stored in an MVar.
\item To allow for timed events, network events and application events to affect an association, associations are stored in an MVar and timers are run in separate threads.
\item To create a cookie the getCurrentTime is called. The timers call getCPUTime for small grained timing.
\item To secure associations randomIO is called to generate random numbers.
\item To allow the application interface to have side effects, the event handler is also an IO function.
\end{itemize}

% The threads and MVars have to do with concurrency. The send and receive have to do with input and output. The timers have to do with error detection and recovery. The system calls are interfacing with external libraries and components. 

Separating the I/O from the pure logic was a goal from the start, but was done only when the stack was functioning correctly as a refactoring. It is seductive to include even the pure logic in the do blocks and effectively have all logic be inside of IO monads. If this separation would not have been a design goal, it might have not been done and the benefits, like more readable code,  would not have been reaped.

Another example of the benefits of splitting the I/O and the pure functions is that the pure functions are very easy to test. Haskell has a quickcheck\cite{Claessen_2000} function that runs random data through the functions to verify properties which can be expressed neatly. This made verifying the message parsing functions and the cookie creation function easy to do. It also aids debugging, when a bug is suspected an extra property is easily defined and verified.

% JK: Inleiding begint met voordelen van Haskell, wat is daar van over?

The implementation looks a lot like it would in any regular imperative language, assignments and I/O calls are sequentially ordered and the operations on the IO values are very close to the source of those values. This makes it easy to see how data flows through.

A possible disadvantage is that it might distract the programmer of one of the merits of the functional programming language, the philosophy of composing functions rather than sequencing actions, wasting Haskell's facilities for function composition. 

\begin{lstlisting}[caption={The connect function looks imperative}]
connect :: SCTP -> NS.SockAddr -> (Event -> IO()) -> IO Socket
connect stack peerAddr eventhandler = do
    keyValues <- replicateM 4 (randomIO :: IO Int)
    myVT <- liftM fromIntegral (randomIO :: IO Int)
    myPort <- liftM fromIntegral $ do 
        let portnum = testUdpPort + 1
        return portnum -- TODO obtain portnumber

    let myAddr = sockAddr (address stack, fromIntegral myPort)
    associationMVar <- newEmptyMVar
    initTimer <- startTimer 10

    let socket = makeConnectionSocket stack myVT
                    associationMVar myAddr
                    eventhandler peerAddr
    let association' = makeAssociation socket (myVT) 
                           myPort peerAddr initTimer
    putMVar (association socket) association'
    registerSocket stack myAddr socket

    let initMessage = makeInitMessage myVT myPort peerAddr
    socketSendMessage socket 
        (ipAddress peerAddr, portNumber peerAddr) initMessage
    registerTimer initTimer 0 
        (initRetransmit association' initMessage 0)
        (associationTimeOut association')
    return socket
\end{lstlisting}

One I/O implementation specific detail that leaks into the otherwise pure types is the concurrent state primitive MVar. The the association has an MVar member which allows both the data receiving thread and the timers to mutate its state safely. Ideally the association would not be contaminated with this implementation specific information.

%\paragraph{Conclusion}

% -- why split I/O and pure
% -- why eventhandler?
% -- planning, type driven development
%Hurdles:
% -- eliminating message channels (aka why eventhandler)
% -- Keeping state, eliminating mvars
%Advantages:
% -- End result seems readable, plain to see all I/O, I/O is all the observable behaviour.
%Testing:
% -- Using quickcheck
% -- Testing the I/O
% -- Debugging

% Analysis:
% Mc-Cabe’s cyclomatic number,  Halstead’s  programming  effort, statement  count, and Oviedo’s data flow complexity satisfy our properties
% -- where lies the complexity?
% -- where is the boilerplate, what is hidden?
% -- where are the bugs, missing features?
% -- Haskell performance \cite{epstein_haskell_????}

\section{Stream-based style}
\paragraph{Implementation}
Since the old way of stream-based I/O is no longer built into the modern Haskell compilers an implementation of stream-based I/O in terms of the I/O monad was made for the exploration of the stream-based style.
In the old Haskell style the responses in the inputstream arrived in the same order they were requested. Although this allowed for convenient lazy pattern matching, it also meant that either the operating system had to execute every I/O action sequentially, or had to reorder the actions regardless of whether the order of the results was of importance. For the new stream-based I/O system the decision was made to not reorder the events, instead passing them through in the order they are returned.
The programmer writing a program using this system defines a handler function that gets called for each event with the previous state, and returns a new state and a list of actions to be performed.
\begin{lstlisting}[caption={Stream-based example}]
handler 0 StartEvent =
    (1, [ReadChan stdin])
handler 1 [ReadResult result] =
    (1, [WriteChan stdout "Hello " ++ result ++ "!", Stop])
\end{lstlisting}
A set of basic I/O operations with their response events have also been implemented.
To start processing the program calls the handlerLoop function with their handler function as a parameter.
The handlerLoop function defines a channel and it listens on it for events. Whenever an event comes in the handler function is called with that event as a parameter along with the previous state. The handler function returns a list of actions that the handlerLoop executes in separate threads. 
\begin{lstlisting}[caption={The eventLoop}]
eventLoop :: (Action a e, Event e) =>
    Handler a e s -> Chan e -> s -> e -> IO ()
eventLoop handler events s event = do
    let (s', actions) = handler s event
    mapM_ (\a -> forkIO $
        handleIO (writeChan events) a) actions
    let stop = stopAction `elem` actions
    unless stop $
        eventLoop handler events s' =<< readChan events
    return ()
\end{lstlisting}
The actions write their results wrapped in events to the channel. This allows all I/O do be performed concurrently.
\begin{lstlisting}[caption={Example of an Action, SendUdpMessage}]
handleIO eventer (SendUdpMessage sock bytes addr) = do
    n <- NSB.sendTo sock bytes addr
    eventer $ SentUdpMessage n
\end{lstlisting}
\paragraph{Architecture}
Like in the monadic style, the architecture of the stream-based implementation is based around an event handler. A single thread waits for incoming data and timer events, when an event is relevant to the interfacing program it is notified by emitting an event to the event handler of the socket that handled the event.
\begin{lstlisting}[caption={The payload handler}]
\end{lstlisting}

\paragraph{Operation}
The SCTP handler starts with the Setup state, from which it sets up an operating system socket and goes to the Established state. It then listens for three kinds of events.

The first is the GotUdpMessage event that contains a newly received data packet. It deserializes the message, checks if it is valid and then passes it to socketAcceptMessage, which is similar to the monadic socketAcceptMessage.

\begin{lstlisting}[caption={The GotUdpMessage event handler}]
handler :: IO.Handler Action Event State
handler s@(Setup address) Begin =
    (s, [MakeUdpSocket address])
handler s@(Setup address) (MadeUdpSocket sock) =
    (Established address sock Map.empty [],
        [ListenOnSocket sock maxMessageSize])
handler s@(Established listenAddress sock sockets hs)
            (GotUdpMessage (bytes,peerAddress)) =
    (Established listenAddress sock sockets' hs, actions)
  where
    message = deserializeMessage bytes
    valid = verifyChecksum message
    destination = (ipAddress listenAddress,
        destinationPortNumber $ header message)
    noop = (sockets,[])
    (sockets', actions) | valid =
        case Map.lookup destination sockets of
            Just socket -> handleMessage socket
            Nothing -> noop
                        | otherwise = noop
    handleMessage socket = (sockets'', actions)
      where
        (socket', actions) = socketAcceptMessage socket
            (ipAddress peerAddress) message
        sockets'' = Map.insert destination socket' sockets
\end{lstlisting}

The second and third are the listen and connect events, they indicate that an interfacing program wants to create a socket. The socket is created and added to the state.

All other events are passed through two sets of handlers. The first set is the handlers of sockets that are being created, the second set is the handlers of established sockets. An event is tested on each handler until a handler accepts it, when it accepts it it returns a new state and a list actions and the handler is removed from the list of handlers.

\begin{lstlisting}[caption={Dispatching to handlers}]
handler s@(Established l sock sockets handlers) event =
    (Established l sock sockets' handlers', actions)
  where
    (handlers', sockets', actions) = dispatch handlers []
    dispatch [] hs_ = dispatchMap hs_ $ Map.toList sockets
    dispatch (h:hs) hs_ = case h event of
           Just (AResult (socket, actions)) ->
               (hs_ ++ hs, sockets', actions)
             where
               sockets' = Map.insert k socket sockets
               k = (ipAddress $ socketAddress socket,
                   fromIntegral $ portNumber $
                                  socketAddress socket)
           Just (AHandler handler) ->
               (handler:hs_ ++ hs, sockets, actions)
           Nothing -> dispatch hs (h:hs_)
    dispatchMap handlers [] = (handlers,sockets, [])
    dispatchMap handlers ((k,socket):sockets) =
        case socketHandler socket event of
            Just (socket', actions) -> 
                (handlers, sockets', actions)
              where
                sockets' = Map.insert k socket'
                            (Map.fromList sockets)
            Nothing -> dispatchMap handlers sockets
\end{lstlisting}

As one can see the whole dispatching to handlers code is rather intricate. 

\paragraph{Analysis}
The first thing that stands out from this implementation is that there is a lot of code involved in dispatching events to handlers and correctly returning a new state with new handlers and actions. What makes the system fairly complex is that it has one handler that is the entry point of the application but it has many handlers in different places that events could be meant for. The idea of decoupling the request from the response this way has certainly moved a lot of complexity from the operating system to the programmer.

The obvious advantage to this is that it is per default concurrent. For example the handleInit function performs 3 I/O actions in parallel. The monadic counterpart of this function performs these functions sequentially. Depending on the resources this might significantly speed up the function though in this case it is unlikely as the cost of writing and reading from the channel three times is probably more expensive than the usually immediate RandomInteger and GetTime actions. 

\begin{lstlisting}[caption={The init handler}]
handleInit socket@ConnectSocket{} _ _ = (socket, [])
handleInit socket@ListenSocket{..} address message =
    (socket', actions)
  where
    socket' = socket { 
        socketHandlers = (socketHandlers) ++ [acc]
    }
    actions = [RandomInteger, RandomInteger, GetTime]
    acc = initAccumulator handle
    handle s (Just i, Just j, Just t) = (s,actions')
      where
        portnum = fromIntegral $ (sourcePortNumber.header) message
        message' = makeInitResponse address message socketSecret t i j
        actions' = [socketSendMessage s destination message']
        destination = sockAddr (address, portnum)
\end{lstlisting}

The handleInit function makes use of a function called initAccumulator, this function accumulates the events returned by the action and passes them on to the handle function which uses them to construct the init response message.

\begin{lstlisting}[caption={The init handler}]
accumulate acc handler state event = case acc event of
   Accumulated result -> Just (handler state result)
   Accumulating acc' -> Just (state {
        socketHandlers = socketHandlers state ++
           [accumulate acc' handler]} ,[])
   Skipped -> Nothing

initAccumulator = accumulate (initAccumulator_ 
                             (Nothing, Nothing, Nothing))
initAccumulator_ (Nothing, Nothing, t) (GotRandomInteger i)=
    initAccumulator' (Just i, Nothing, t)
initAccumulator_ (i, Nothing, t) (GotRandomInteger j) =
    initAccumulator' (i, Just j, t)
initAccumulator_ (i, j, Nothing) (Time t) =
    initAccumulator' (i,j,Just t)
initAccumulator_ _ _ = Skipped
initAccumulator' t@(Just _, Just _, Just _) = Accumulated t
initAccumulator' t = Accumulating (initAccumulator_ t)
\end{lstlisting}

It should be noted that the cumbersome accumulating of unordered events could possibly be reduced by introducing helper functions that employ type introspection or other metaprogramming to create syntactic sugar like was done for the monadic style. 

Something that has disappeared when comparing to the monadic style is the dealing with concurrency. Since concurrency has been abstracted away by the events the MVars were no longer necessary and all datastructures are clean from mutable state.
Especially socketAcceptMessage, which had to deal with sockets in MVars in the monadic style has benefitted from this and is significantly simpler in the stream based implementation.
